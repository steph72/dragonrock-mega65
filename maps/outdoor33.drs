
includemap "maps/outdoor33.drm"

$ msgWaitkey,"\n-- key -- "


; ----------- main road --------------------------

connectLabel mainRoad2: 13,2 - 24,2
connectLabel mainRoad3: 24,3 - 26,3
connectLabel mainRoad4: 26,4 - 31,4

$ msgMainRoad,"""
A wide and comfortable road.
"""

mainRoad1:
mainRoad2:
mainRoad3:
mainRoad4:
    NSTAT_O msgMainRoad
    ---

; -------------------------------------

connectLabel fhPath1: 10,14 - 14,14
connectLabel fhPath2: 14,15 - 20,15
connectLabel terrorShack: 10,2 - 11,2
connectLabel terrorShackEntry: 10,1 - 11,1
connectLabel dirtRoad: 10,4
connectLabel mountainPath: 10,3

$ msgFhPath,"A narrow driveway"

$ msgTerrorShack,"""
A set of wooden doors has been built
into the mountain, apparently not long 
ago. A crude sign on the door says:
'THE NOYSY PIG RIDERZ. KEEP OUT!'
"""

$ msgDirtRoad,"""
You notice an unusual amount of rubble 
and junk piling up at the north side  
of the road.
"""

$ msgMountainPath,"""
A small path leads to the mountains
in the north.
"""

fhPath1:
fhPath2:
    NSTAT_O msgFhPath
    ---

dirtRoad:
    NSTAT_O msgDirtRoad
    ---

mountainPath:
    NSTAT_O msgMountainPath
    ---

terrorShack:
    NSTAT_O msgTerrorShack
    ---

$ msgTerrorShackEntry,"Enter new location (y/n)?"

terrorShackEntry:
    DISP_S msgTerrorShackEntry,True
    YESNO_B enterShack
    REDRAW
    ---

enterShack:
    ENTER_D 1,10,18   ;load dungeonMap 1
    ---

; -------------------------------------
; -------- troll bridge ---------------
; -------------------------------------

connectLabel lTrollBridge: 16,15

defDaemon bikerGangDaemon: 9,2 - 15,16

$ msgBridgeInitial1,"""
As you arrive at the bridge, you meet 
a group of adolescent trolls. Some of 
them are hanging around and kicking 
the dirt, laughing stupidly. Others 
are still mounted on their riding 
pigs, occasionally giving their 
mounts' ears a violent twist, causing 
them to squeal in pain.

"""

$ msgBridgeInitial2,"""
One of the trolls stands apart from
the others. He's wearing a black
leather armor and he's sitting atop
a particularly ugly pig. He rides
up to you and proclaims:

'ME'S LUKASZ, DIZ MY BRIDGE, DIZ MY 
BOYZ! WE'S RIDING DA NOISY PIGS! 
WE'S THE GREATEST! YOU WANNA PASS, 
YOU GONNA PAY. 50 GOLD!'

"""

$ msgBridge,"""
It's Lukasz' boys again. Lukasz can
be seen somewhere in the background,
zoooming around on his riding pig,
making it squeal from time to time.

His cronies step up to face you.

"""

$ msgPayQuestion,"""
Do you pay Lukasz' toll?"""

$ msgPay,"""

They laugh stupidly, then they 
let you go. As you pass them, they
make their pigs squeal loudly.

"""

$ msgLukasUrlaub,"""
The bridge feels strangely quiet and
peaceful.
"""

$ msgLukasCoward,"""

As the other trolls charge towards 
you, Lukasz spurs his ugly riding 
pig, drops back and vanishes 
from view.

"""

$ msgLukasCoward2,"""

They charge and attack you. Again,
Lukasz himself vanishes from view.

"""

bikerGangDaemon:
    RANDOM_B 100, lBikerGangAttack   ; 10% chance
    ---

$ msgBikersAttack, "Pig riding trolls attack!"
lBikerGangAttack:
    DISP_S msgBikersAttack
    WKEY msgWaitkey
    REDRAW
    ---

lTrollBridge:
    DISP msgBridgeInitial1,True
    WKEY msgWaitkey
    DISP msgBridgeInitial2,True
    WKEY msgWaitkey
    ALTER lTrollBridge, lTrollBridgeLater, 18
    DISP msgPayQuestion,True
    YESNO_B payday
    DISP msgLukasCoward
    WKEY msgWaitkey
    CLRENC
    ADDENC 2,1,2,3,1
    GOENC wWin,wLose
wWin:
wLose:
    REDRAW
    ---

lTrollBridgeLater:
    RANDOM_B 400,lLukasUrlaub ; 40% chance that the boys are not there
    DISP msgBridge,True
    DISP msgPayQuestion
    YESNO_B payday
    DISP msgLukasCoward2
    WKEY msgWaitkey
    REDRAW
    ---

lLukasUrlaub:
    NSTAT_O msgLukasUrlaub
    ---

payday:
    DISP msgPay
    WKEY msgWaitkey
    REDRAW
    ---

; ---------- foxhome ------------

connectLabel city_foxhome: 22,15 - 23,15
connectLabel city_road: 21,15
connectLabel foxhome_signpost: 9,14

$ msgCity,"""
The village of Foxhome. Do you want
to enter? (yes/no):"""

$ msgCityRoad,"""
To the east lies the quiet little
village of Foxhome.
"""

$ msgFoxhomeSignpost,"""
A signpost here says:
'Foxhome - 60 miles east'
"""

foxhome_signpost:
    NSTAT_O msgFoxhomeSignpost
    ---

city_road:
    NSTAT_O msgCityRoad
    ---

city_foxhome:
    DISP msgCity,True
    YESNO enterCity
    REDRAW
    ---

enterCity:
    ENTER_C 0
    ---